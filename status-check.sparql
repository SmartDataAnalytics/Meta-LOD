PREFIX eg: <http://www.example.org/>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>
PREFIX schema: <https://schema.org/>

CONSTRUCT {
  GRAPH ?g {
    ?s
      a sd:Service ;
      sd:endpoint ?e ;
      schema:serverStatus ?status ;
      schema:dateModified ?time ;
      .
  }
}
{
  BIND(NOW() AS ?time)

  GRAPH ?g { ?s sd:endpoint ?e }

  OPTIONAL {
    SERVICE SILENT ?e {
      { SELECT ?t { ?x ?y ?t } LIMIT 1 }
    }
  }

  # This is a best-effort query; it assumes at least one type triple in the default graph
  # It should work for 99% of the cases
  # The issue is, that I don't trust legacy endpoints to handle ?s ?p ?o well
  # (especially mysql based ones: first copy everything into a temporary table of everything, than pick the 1 result...)
  # And some may even complain about exceeded execution time estimations
  BIND(IF(BOUND(?t), schema:Online, schema:OfflineTemporarily) AS ?status)
}
